plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'nu.studer.jooq' version '8.2'
}

group = 'com.jordansimsmith'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

sourceSets {
    main {
        java.srcDirs += '/build/generated-src/jooq'
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        targetExclude '/build/generated-src/**/*'
        googleJavaFormat().aosp()
        formatAnnotations()
    }
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/immersion_tracker'
                    user = 'immersion_tracker_user'
                    password = 'password'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = false
                        fluentSetters = false
                    }
                    target {
                        packageName = 'com.jordansimsmith.immersion.tracker.jooq'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-jooq:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.1.4'
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'org.jfree:jfreechart:1.5.4'
    jooqGenerator 'org.postgresql:postgresql:42.6.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.4'
}

tasks.named('test') {
    useJUnitPlatform()
}
